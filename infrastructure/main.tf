############################################################################
# Create resource groups
############################################################################
resource "azurerm_resource_group" "primary" {
  name     = "${local.org}-rg-${local.resource_suffix}"
  location = local.location
  tags     = local.tags
}

############################################################################
# Create storage account
############################################################################

resource "azurerm_storage_account" "redaction_storage" {
  #checkov:skip=CKV_AZURE_33: Logging not implemented yet
  #checkov:skip=CKV2_AZURE_1: Customer Managed Keys not implemented
  #checkov:skip=CKV_AZURE_206: Replication not needed
  #checkov:skip=CKV2_AZURE_40: Enable key-based authentication to allow ADO to access the storage account
  name                             = "${local.org}stredaction${var.environment}${local.location_short}"
  resource_group_name              = azurerm_resource_group.primary.name
  location                         = local.location
  account_tier                     = "Standard"
  account_replication_type         = var.storage_account_replication_type
  account_kind                     = "StorageV2"
  min_tls_version                  = "TLS1_2"
  allow_nested_items_to_be_public  = "false"
  cross_tenant_replication_enabled = "false"
  shared_access_key_enabled        = true
  default_to_oauth_authentication  = true
  public_network_access_enabled    = false
  https_traffic_only_enabled       = true
  tags                             = local.tags

  blob_properties {
    delete_retention_policy {
      days = var.storage_retention_days
    }
  }

  lifecycle {
    prevent_destroy = true
  }

  sas_policy {
    expiration_period = "01.12:00:00"
  }

  network_rules {
    default_action = "Deny"
    bypass         = ["AzureServices"] # Keep Azure platform services in scope
  }
}

resource "azurerm_storage_container" "redaction_storage" {
  #checkov:skip=CKV2_AZURE_21: Not needed
  for_each = local.storage_containers

  name                  = each.key
  storage_account_id    = azurerm_storage_account.redaction_storage.id
  container_access_type = "private"
}

############################################################################
# Create Azure Function App
############################################################################

resource "azurerm_service_plan" "redaction_system" {
  #checkov:skip=CKV_AZURE_212: TODO: Limit reached in subscription
  #checkov:skip=CKV_AZURE_225: TODO: Limit reached in subscription
  name                = "${local.org}-asp-${local.resource_suffix}"
  resource_group_name = azurerm_resource_group.primary.name
  location            = local.location
  os_type             = "Linux"
  sku_name            = "EP1"
  #worker_count           = 2
  #zone_balancing_enabled = true
}

resource "azurerm_linux_function_app" "redaction_system" {
  name                = "${local.org}-func-${local.resource_suffix}"
  resource_group_name = azurerm_resource_group.primary.name
  location            = local.location

  storage_account_name          = azurerm_storage_account.redaction_storage.name
  storage_account_access_key    = azurerm_storage_account.redaction_storage.primary_access_key
  service_plan_id               = azurerm_service_plan.redaction_system.id
  public_network_access_enabled = false
  virtual_network_subnet_id     = azurerm_subnet.function_app.id
  https_only                    = true

  site_config {
    application_stack {
      python_version = var.function_app_python_version
    }
    application_insights_key = azurerm_application_insights.redaction_system.instrumentation_key
    cors {
      allowed_origins = ["https://portal.azure.com"]
    }
  }
  identity {
    type = "SystemAssigned"
  }
  app_settings = {
    #"WEBSITE_CONTENTAZUREFILECONNECTIONSTRING" = "DefaultEndpointsProtocol=https;AccountName=${azurerm_storage_account.redaction_storage.name};AccountKey=${azurerm_storage_account.redaction_storage.primary_access_key};EndpointSuffix=core.windows.net"
    "WEBSITE_CONTENTSHARE" : "${local.org}-func-${local.resource_suffix}"
    "SCM_DO_BUILD_DURING_DEPLOYMENT" = "true"
    "OPENAI_ENDPOINT"                = azurerm_cognitive_account.open_ai.endpoint
    "AZURE_VISION_ENDPOINT"          = azurerm_cognitive_account.computer_vision.endpoint
    "ENV"                            = var.environment
    "APP_INSIGHTS_CONNECTION_STRING" = azurerm_application_insights.redaction_system.connection_string
    "WEBSITE_CONTENTOVERVNET"        = 1
  }
}

############################################################################
# Create App Insights
############################################################################
resource "azurerm_log_analytics_workspace" "redaction_system" {
  name                = "${local.org}-log-${local.resource_suffix}"
  location            = local.location
  resource_group_name = azurerm_resource_group.primary.name
  sku                 = "PerGB2018"
  retention_in_days   = var.log_retention_days
  daily_quota_gb      = var.daily_log_cap

  tags = local.tags
}

resource "azurerm_application_insights" "redaction_system" {
  name                = "${local.org}-ai-${local.resource_suffix}"
  location            = local.location
  resource_group_name = azurerm_resource_group.primary.name
  application_type    = "other"
  retention_in_days   = var.log_retention_days
  workspace_id        = azurerm_log_analytics_workspace.redaction_system.id

  tags = local.tags
}

############################################################################
# Create Azure Open AI
############################################################################
resource "azurerm_cognitive_account" "open_ai" {
  #checkov:skip=CKV2_AZURE_22: Customer Managed Keys not implemented
  name                               = "${local.org}-openai-${local.resource_suffix}"
  location                           = local.location
  resource_group_name                = azurerm_resource_group.primary.name
  kind                               = "OpenAI"
  sku_name                           = "S0"
  custom_subdomain_name              = "${local.org}-openai-${local.resource_suffix}"
  public_network_access_enabled      = false
  outbound_network_access_restricted = true
  fqdns                              = ["azureprivatedns.net"]
  local_auth_enabled                 = false
  identity {
    type = "SystemAssigned"
  }
}

############################################################################
# Create Azure Computer Vision
############################################################################
resource "azurerm_cognitive_account" "computer_vision" {
  #checkov:skip=CKV2_AZURE_22: Customer Managed Keys not implemented
  name                               = "${local.org}-cv-${local.resource_suffix}"
  location                           = local.location
  resource_group_name                = azurerm_resource_group.primary.name
  kind                               = "ComputerVision"
  sku_name                           = "F0"
  custom_subdomain_name              = "${local.org}-computervision-${local.resource_suffix}"
  public_network_access_enabled      = false
  outbound_network_access_restricted = true
  fqdns                              = ["azureprivatedns.net"]
  local_auth_enabled                 = false
  identity {
    type = "SystemAssigned"
  }
}

##
# Service bus
##
resource "azurerm_servicebus_namespace" "redaction" {
  #checkov:skip=CKV_AZURE_199: Microsoft managed keys are acceptable
  #checkov:skip=CKV_AZURE_201: Microsoft managed keys are acceptable
  #checkov:skip=CKV_AZURE_204: public network access only enabled in dev
  name                          = "${local.org}-sb-${local.resource_suffix}"
  location                      = local.location
  resource_group_name           = azurerm_resource_group.primary.name
  sku                           = var.service_bus_premium_enabled ? "Premium" : "Standard"
  capacity                      = var.service_bus_premium_enabled ? 1 : 0
  premium_messaging_partitions  = var.service_bus_premium_enabled ? 1 : null
  minimum_tls_version           = "1.2"
  local_auth_enabled            = false
  public_network_access_enabled = !var.service_bus_premium_enabled
  identity {
    type = "SystemAssigned"
  }

  tags = local.tags
}

resource "azurerm_servicebus_topic" "redaction_process_complete" {
  name         = "redaction_process_complete"
  namespace_id = azurerm_servicebus_namespace.redaction.id

  partitioning_enabled = true
}

resource "azurerm_servicebus_subscription" "redaction_process_complete" {
  for_each           = local.redaction_process_subscribers
  name               = each.key
  topic_id           = azurerm_servicebus_topic.redaction_process_complete.id
  max_delivery_count = 1
}

resource "azurerm_servicebus_subscription_rule" "redaction_process_complete" {
  for_each        = local.redaction_process_subscribers
  name            = "subscription_rule"
  subscription_id = azurerm_servicebus_subscription.redaction_process_complete[each.key].id
  filter_type     = "CorrelationFilter"
  correlation_filter {
    label = each.key
  }
}
